#!/usr/bin/perl

use warnings;
use strict;
use Socket;
use Getopt::Long;


my %opt;
GetOptions(\%opt, 'exclude=s');
my @exclude;
@exclude = split /\s+/, $opt{exclude} if $opt{exclude};

my $scp = 'scp';


my $cmd;

$cmd = "$scp balance1:/etc/eq.conf /tmp/balance1.eq.conf";
system($cmd) == 0 or die "Error: $cmd\n";

$cmd = "$scp livedb:/var/lib/interchange/etc/sync-tables.conf /tmp/livedb.sync-tables.conf";
system($cmd) == 0 or die "Error: $cmd\n";


my $something_amiss;

my %balance_servers;
@ARGV = '/tmp/balance1.eq.conf';
my ($in, $server);
while (<>) {
	$in = $1, $server = 0, next if /^cluster\s*(\S+)\s*{\s*$/;
	$server = 1, next if $in and /^\s*server\s*\S+\s*{\s*$/;
	next unless $in and $server and /^\s*ip\s*=\s*"(\d+\.\d+\.\d+\.\d+)";\s*$/;
	my $ip = $1;
	my $ipaddr = inet_aton($ip);
	my $name = gethostbyaddr($ipaddr, AF_INET);
	$name =~ s/\..*//;
	$balance_servers{$name || $ip} = 1;
}
delete @balance_servers{@exclude};

my %dbpush_servers;
@ARGV = '/tmp/livedb.sync-tables.conf';
while (<>) {
	next unless /^\s*\*\s*push\(([^)]+)\)\s*:\s*livedb\s*->\s*(.*)\s*$/;
	my ($push, $servers) = ($1, $2);
	@dbpush_servers{ split(/\s*,\s*/, $servers) } = ();
}
delete @dbpush_servers{@exclude};

my %filepush_servers;
@ARGV = '/var/lib/interchange/bin/push-further';
while (<>) {
	next unless /^\s*servers="([^"]+)"\s*$/;
	@filepush_servers{ split(/\s+/, $1) } = ();
}
delete @filepush_servers{@exclude};

print "Servers active in load balancers (regardless of static weight):\n";
print join ' ', sort keys %balance_servers;
print "\n\n";

my ($m1, $m2);

print "Servers being pushed to in livedb:~interch/etc/sync-tables.conf:\n";
print join ' ', sort keys %dbpush_servers;
print "\n";
($m1, $m2) = missing([keys %balance_servers], [keys %dbpush_servers]);
if (@$m2) {
	print "\n*** MISSING SERVER PROBLEM!\n";
	print "These servers are active in the load balancers\n";
	print "but their database isn't being updated:\n";
	print join ' ', @$m2;
	print "\n";
}
if (@$m1) {
	print "\n*** Extra server warning!\n";
	print "These servers are not active in the load balancers\n";
	print "but their database is being updated anyway:\n";
	print join ' ', @$m1;
	print "\n";
}
$something_amiss = 1 if @$m1 or @$m2;
print "\n";

print "Servers being pushed to in standby:~interch/bin/push-further:\n";
print join ' ', sort keys %filepush_servers;
print "\n";
($m1, $m2) = missing([keys %balance_servers], [keys %filepush_servers]);
if (@$m2) {
	print "\n*** MISSING SERVER PROBLEM!\n";
	print "These servers are active in the load balancers\n";
	print "but their files aren't being updated:\n";
	print join ' ', @$m2;
	print "\n";
}
if (@$m1) {
	print "\n*** Extra server warning!\n";
	print "These servers are not active in the load balancers\n";
	print "but their files are being updated anyway:\n";
	print join ' ', @$m1;
	print "\n";
}
$something_amiss = 1 if @$m1 or @$m2;
print "\n";

print "*** The push configurations look ok!\n\n" unless $something_amiss;


sub missing {
	my ($A, $B) = @_;
	my (@missing_A, @missing_B);
	my %B;
	@B{@$B} = ();
	for (@$A) {
		push @missing_B, $_ unless exists $B{$_};
	}
	my %A;
	@A{@$A} = ();
	for (@$B) {
		push @missing_A, $_ unless exists $A{$_};
	}
	return (\@missing_A, \@missing_B);
}
