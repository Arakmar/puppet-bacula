#!/usr/local/bin/perl

# rcdiff = Remote Command diff
#
#  This script will execute a given command (or set of commands)
#  on different servers and then diff the output of those commands.
#
# Usage: rcdiff server1:"command" server2[:"command"]
#
# Note: This depends on your own ssh key or password access
#       to each machine.  There is no ssh key specific to this 
#       script.


use strict;
use warnings;


# Get arguments
my ($first, $second) = @ARGV;

if (!$first || !$second) {
    die "Missing servers to diff!";
}

# Don't buffer output
$| = 1;

# Store temp files to be cleaned up later
my @tmp_files;

# Get the first server/command pair
my $first_cmd_file;
my $first_cmd;
if ($first =~ /:/) {
    my ($server, $cmd) = split /:/, $first;

    $first_cmd_file = "$server-". substr($cmd,0,16) .".tmp$$-1";
    $first_cmd_file =~ s/[^\w\-\.\d]/_/g;
    $first_cmd_file = "/tmp/$first_cmd_file";

    $first_cmd = $cmd;
    my $ssh_cmd = "ssh $server '$cmd'";
    my $rv = `$ssh_cmd`;
    if ($?) {
        print "Error running command on server $server: ERROR=$?";
    }
    open (my $f_fh, '>', $first_cmd_file) or die "Could not open $first_cmd_file: $!";
    print {$f_fh} $rv, $/;
    close($f_fh);

    push @tmp_files, $first_cmd_file;
}
else {
    die "Missing command to run on first server!";
}


# Get the second
my ($second_cmd_file, $second_server, $second_cmd);
if ($second =~ /:/) {
    ($second_server, $second_cmd) = split /:/, $second;
}
else {
    ($second_server, $second_cmd) = ($second, $first_cmd);
}

$second_cmd_file = "$second_server-". substr($second_cmd,0,16) .".tmp$$-2";
$second_cmd_file =~ s/[^\w\-\.\d]/_/g;
$second_cmd_file = "/tmp/$second_cmd_file";

my $ssh_cmd = "ssh $second_server '$second_cmd'";
my $rv = `$ssh_cmd`;
if ($?) {
    print "Error running command on server $second_server: ERROR=$?";
}
open (my $f_fh, '>', $second_cmd_file) or die "Could not open $second_cmd_file: $!";
print {$f_fh} $rv, $/;
close($f_fh);

push @tmp_files, $second_cmd_file;


# Diff the two files
my $diff = `diff -u $first_cmd_file $second_cmd_file`;
print $diff, $/;


# Clean up any tmp files generated
END {
   -f $_ && unlink($_)  foreach (@tmp_files);
}


